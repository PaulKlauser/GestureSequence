task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
ext["signing.secretKeyRingFile"] = rootProject.rootDir.absolutePath + "/" + System.getenv('GPG_KEY_PATH')
ext["sonatypeUsername"] = System.getenv('SONATYPE_USERNAME')
ext["sonatypePassword"] = System.getenv('SONATYPE_PASSWORD')

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'io.github.paulklauser'
                artifactId = 'gesturesequence'
                pom {
                    name = 'Gesture Sequence'
                    description = 'A basic library that enables easy composition of gesture sequence recognition on a view.'
                    url = 'https://github.com/PaulKlauser/GestureSequence'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            id = 'PaulKlauser'
                            name = 'Paul Klauser'
                            email = 'paul.klauser@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://git@github.com:PaulKlauser/GestureSequence.git'
                        developerConnection = 'scm:git:ssh://git@github.com:PaulKlauser/GestureSequence.git'
                        url = 'https://github.com/PaulKlauser/GestureSequence/tree/main'
                    }
                }
                artifact androidSourcesJar
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
    signing {
        sign publishing.publications.release
    }
}